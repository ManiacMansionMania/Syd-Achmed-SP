// Pizza Shack (von außen) (Syd)

bool b_ShackOffen = false;
/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
	cJeff.FaceDirection(eDirectionUp);
	oTuer.init_object(11);
}

function room_AfterFadeIn()
{
	PlayAmbientSound(1, 101, 255, 155, 140);
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/



/****************************
*   Objekt-Funktionen
****************************/



/****************************
*   Hotspot-Funktionen
****************************/
function hEingang_AnyClick()
{
	if ( b_ShackOffen ) // dritte Zeile oben
	{
		if ( any_click_on_door(11, oTuer, 236, 135, eDirectionUp, 24, 39, 134, eDirectionRight) == 0 ) Unhandled();	
	}
	else
	if (any_click_move(269, 135, eDirectionUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("'Woodie's Pizza Shack'."))
				player.Say("Der Eingang zu 'Woodie's Pizza Shack'.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Warum?"))
				player.Say("Gute Idee, aber es hapert an der Umsetzung.");
		}
		else if (UsedAction(eActUse) || UsedAction(eActOpen))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Zu!");
				Wait (50);
				cJeff.FaceDirection(eDirectionRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend, Achmed.");
				player.FaceDirection(eDirectionLeft);
				cJeff.Say("Wenn du 'ne Pizza willst, dann komm' hier rüber.");
				player.Say("Später vielleicht.");
				cJeff.Say("Wie du meinst ...");
				cJeff.FaceDirection(eDirectionUp);				
			}
			else if ( cSyd == player )
			{
				player.Say("Geschlossen!");
				Wait (50);
				cJeff.FaceDirection(eDirectionRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend, Syd.");
				player.FaceDirection(eDirectionLeft);
				cJeff.Say("Wenn du 'ne Pizza willst, dann komm' hier rüber.");
				player.Say("Vielleicht später.");
				cJeff.Say("Wie du meinst ...");
				cJeff.FaceDirection(eDirectionUp);
			}
			else
			{
				cJeff.FaceDirection(eDirectionRight);
				cJeff.Say("Unser Lokal öffnet erst heute abend.");
				cJeff.FaceDirection(eDirectionUp);				
			}
		}
		else if (UsedAction(eActClose) || UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			cJeff.FaceDirection(eDirectionRight);
			cJeff.Say("Was machst du da?");
			player.FaceDirection(eDirectionLeft);
			player.Say("Ähh.");
			cJeff.Say("Lass das brav bleiben.");
			Wait(20);
			cJeff.FaceDirection(eDirectionUp);
		}
		else Unhandled();
	}
}

function hPlakat_AnyClick()
{
	if (any_click_move(158, 133, eDirectionUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt) || UsedAction(eActUse))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Leere Plakate das sein.");
				player.FaceDirection(eDirectionDown);
				player.Say("Das sein gutes Platz für Hinweis in fertiges Eppi.");
			}
			else
			{
				player.Say("Plakate.");
				player.Say("Ohne Aufdruck ...");
				player.FaceDirection(eDirectionDown);
				player.Say("Noch ein guter Platz für einen Hinweis ...");
				player.Say("... in einer fertigen Episode.");
			}
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			if ( cAchmed == player )
			{
				player.Say("Achmed nix nehmen \"Ronville-News\" mit. Achmed sein braves Bürger.");
			}
			else
			{
				player.Say("Dann kann es keiner mehr lesen ...");
				player.Say("... sobald etwas draufsteht.");
			}
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("???"))
				player.Say("Und was soll das bringen?");
		}
		else Unhandled();
	}
}

function hStrasse_AnyClick()
{
	if (any_click_move(308, 134, eDirectionRight))
	{
		if (UsedAction(eActWalkTo) || UsedAction(eActUse))
		{
			Wait(5);
			player.ChangeRoom(7, 11, 131);
			player.FaceDirection(eDirectionRight);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Weg zu S-Mart-Ashlay."))
				player.Say("Da geht's zum S-Mart.");
		}
		else if (UsedAction(eActPickUp) || UsedAction(eActOpen) || 
						 UsedAction(eActClose) || UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			if ( !cAchmed.SayIfPlayer("Straße sein unkoporzativ.")) // unkooperativ
				player.Say("Das lässt die Straße nicht mit sich machen.");
		}
		else Unhandled();
	}
}


/****************************
*   Regionen-Funktionen
****************************/
